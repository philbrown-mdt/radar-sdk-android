plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
    id 'io.gitlab.arturbosch.detekt' version "$detekt_version"
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 16
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        warningsAsErrors true
    }
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        events = ['passed', 'failed', 'skipped']
    }
}

detekt {
    toolVersion = "$detekt_version"
    ignoreFailures = false
    config = rootProject.files('tools/detekt/radar-detekt-config.yml')
    buildUponDefaultConfig = true
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
        sarif.enabled = false
    }
}

dependencies {
    implementation project(':sdk')
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/docs"))

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dokkaJavadoc.configure {
    outputDirectory.set(file("$buildDir/javadocs"))
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

publishing {
    publications {
        sdkPublication(MavenPublication) {
            groupId "io.radar"
            artifactId "sdk-livedata-ktx"
            version radarVersion
            artifact "$buildDir/outputs/aar/$project.name-release.aar"
            artifact androidSourcesJar
            artifact androidJavadocsJar

            pom {
                name = "Radar Android SDK LiveData Kotlin Extensions"
                description = "LiveData Kotlin Extensions for the Android SDK for Radar, the leading geofencing and location tracking platform"
                url = "https://radar.io"

                licenses {
                    license {
                        name = "Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                organization {
                    name = "Radar Labs, Inc."
                    url = "https://radar.io"
                }

                developers {
                    developer {
                        id = "radarlabs"
                        name = "Radar Labs, Inc."
                    }
                }

                scm {
                    connection = "scm:git:git://github.com:radarlabs/radar-sdk-android.git"
                    developerConnection = "scm:git:ssh://github.com:radarlabs/radar-sdk-android.git"
                    url = "https://github.com/radarlabs/radar-sdk-android"
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each { dependency ->
                        if (dependency.group == null || dependency.name == null || dependency.version == null) {
                            return
                        }
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.properties["nexusUsername"] != null ? project.properties["nexusUsername"] : ""
                password = project.properties["nexusPassword"] != null ? project.properties["nexusPassword"] : ""
            }
        }
    }
}

signing {
    sign publishing.publications.sdkPublication
}